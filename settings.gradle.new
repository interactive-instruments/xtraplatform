pluginManagement {
    repositories {
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
        gradlePluginPortal()
    }
}

plugins {
    id "org.danilopianini.gradle-pre-commit-git-hooks" version "2.0.13"
}

dependencyResolutionManagement {
    repositories {
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
    }
    versionCatalogs {
        catalog {
            from(files("gradle/plugins.versions.toml"))
        }
        xtraplatform {
            from("de.interactive_instruments:xtraplatform-catalog:5.4.0-refactor-SNAPSHOT")
        }
    }
}

rootProject.name = 'xtraplatform-core'

rootDir.listFiles().each { file ->
    if (file.isDirectory() && file.name.startsWith('xtraplatform-')) {
        include file.name
    }
}

extensions.gitHooks.with {
    preCommit { ctx ->
        ctx.tasks('check', [].toArray(), true)
    }
    createHooks(false)
}

//includeBuild '../xtraplatform-build'
